plugins {
    id 'org.springframework.boot' version '3.0.6'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}
apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

repositories {
    mavenCentral()
}

sourceSets {
    test {
        java.srcDir 'src/test/java'
    }
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

jar {
    version '1.0.0'
    manifest {
        attributes 'Class-Path': 'springboot-gradle-all.jar'
        attributes 'Main-Class': 'tech.mrwconsulting.sample.Application'
    }
}

configurations {
    integrationTestRuntimeOnly.extendsFrom(runtimeOnly)
    integrationTestImplementation.extendsFrom(implementation)
}

task itest(type: Test) {
    classpath = sourceSets.integrationTest.runtimeClasspath
    testClassesDirs = sourceSets.integrationTest.output.classesDirs

    testLogging {
        events "passed", "skipped", "failed"
    }
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude module: 'spring-boot-starter-tomcat'
    }
    implementation('org.springframework.boot:spring-boot-starter-jetty')
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
}
